apply plugin: 'java'
// For spock testing
apply plugin: 'groovy'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    def jdk6Home = System.getenv()['JAVA6_HOME']
    options.bootClasspath = (new File(jdk6Home, "/jre/lib/rt.jar")).canonicalPath
}

repositories {
    mavenCentral()
    // For snapshots of event callback and spock testing
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'javax.inject:javax.inject:1'
    compile 'com.byoutline.eventcallback:eventcallback:1.2.1'

    // For spock testing:
    testCompile 'org.codehaus.groovy:groovy-all:2.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    // allows mocking of classes (in addition to interfaces)
    testRuntime "cglib:cglib-nodep:2.2.2"
    // allows mocking of classes without default constructor (together with CGLIB)
    testRuntime "org.objenesis:objenesis:1.2"
}

gradle.taskGraph.whenReady { taskGraph ->
    Properties p = new Properties()
    p.load(new FileInputStream("push.properties"))
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = p['signing.keyId'] }
        allprojects { ext."signing.password" = p['signing.password'] }
        allprojects { ext."signing.secretKeyRingFile" = p['signing.secretKeyRingFile'] }
    }
}

apply from: 'maven-push.gradle'

//configurations.all {
//    resolutionStrategy.cacheDynamicVersionsFor 1, 'minutes'
//    resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
//}